plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev.legacyforge' version '2.0.74'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

base {
    archivesName = mod_id
    version = mod_version
    group = mod_group_id
}

legacyForge {
    version = "$minecraft_version-$forge_version"

    parchment {
        minecraftVersion = minecraft_version
        mappingsVersion = mapping_version
    }

    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'

            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

repositories {
    maven { url = "https://maven.createmod.net" } // Create, Ponder, Flywheel
    maven { url = "https://maven.ithundxr.dev/mirror" } // Registrate
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" } // ForgeConfigAPIPort
    maven {
        url = "https://chocolateminecraft.com/maven"
        name = "Xaero's Maven"
    }
}

dependencies {
    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
    annotationProcessor "io.github.llamalad7:mixinextras-common:0.4.1"
    modImplementation "io.github.llamalad7:mixinextras-forge:0.4.1"

    modImplementation "xaero.lib:xaerolib-forge-${minecraft_version}:${xaero_lib_version}+"
    modImplementation "xaero.map:xaeroworldmap-forge-${minecraft_version}:${xaero_world_map_version}";

    modImplementation ("com.simibubi.create:create-${minecraft_version}:${create_version}:slim") { transitive = false }
    modImplementation "net.createmod.ponder:Ponder-Forge-${minecraft_version}:${ponder_version}"

    modCompileOnly "dev.engine-room.flywheel:flywheel-forge-api-${minecraft_version}:${flywheel_version}"
    modRuntimeOnly "dev.engine-room.flywheel:flywheel-forge-${minecraft_version}:${flywheel_version}"
    modImplementation "com.tterrag.registrate:Registrate:${registrate_version}"
}

tasks.named("processResources", ProcessResources).configure {
    var replaceProperties = [
            minecraft_version            : minecraft_version,
            minecraft_version_range      : minecraft_version_range,
            forge_version_range          : forge_version_range,
            loader_version_range         : loader_version_range,
            xaero_world_map_version_range: xaero_world_map_version_range,
            create_version_range         : create_version_range,
            mod_id                       : mod_id,
            mod_name                     : mod_name,
            mod_license                  : mod_license,
            mod_version                  : mod_version,
            mod_authors                  : mod_authors,
            mod_description              : mod_description,
            mod_credits                  : mod_credits
    ]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml']) {
        expand replaceProperties
    }
}

mixin {
    add sourceSets.main, "xaerotrainmap.refmap.json"
    config "xaerotrainmap.mixins.json"
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    it.options.release = 17
}

tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
}

jar {
    manifest.attributes([
            "MixinConfigs": "xaerotrainmap.mixins.json",
    ])
}
