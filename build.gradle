plugins {
    id 'java-library'
    id 'maven-publish'
    id "net.neoforged.moddev.legacyforge" version "2.0.74"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

base {
    archivesName = mod_id
    version = mod_version
    group = mod_group_id
}

legacyForge {
    version = "$minecraft_version-$forge_version"

    parchment {
        minecraftVersion = minecraft_version
        mappingsVersion = mapping_version
    }

    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'

            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

repositories {
    //mavenCentral()
    maven { url = "https://maven.createmod.net" } // Create, Ponder, Flywheel
    maven { url = "https://maven.ithundxr.dev/mirror" } // Registrate
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" } // ForgeConfigAPIPort
    maven {
        url = "https://chocolateminecraft.com/maven"
        name = "Xaero's Maven"
    }
}

dependencies {
    compileOnly(annotationProcessor("org.spongepowered:mixin:0.8.5:processor"))

    modImplementation "xaero.lib:xaerolib-forge-1.20.1:1.1.0+"
    modImplementation "xaero.map:xaeroworldmap-forge-1.20.1:1.40.11"

    modImplementation("com.simibubi.create:create-${minecraft_version}:${create_version}:slim") { transitive = false }
    modImplementation "net.createmod.ponder:Ponder-Forge-${minecraft_version}:${ponder_version}"

    modCompileOnly "dev.engine-room.flywheel:flywheel-forge-api-${minecraft_version}:${flywheel_version}"
    modRuntimeOnly "dev.engine-room.flywheel:flywheel-forge-${minecraft_version}:${flywheel_version}"
    modImplementation "com.tterrag.registrate:Registrate:${registrate_version}"

    modCompileOnly annotationProcessor("io.github.llamalad7:mixinextras-common:0.4.1")
    modImplementation "io.github.llamalad7:mixinextras-forge:0.4.1"
}

tasks.named("processResources", ProcessResources).configure {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml']) {
        expand replaceProperties
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    it.options.release = 17
}


tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

javadoc {
    // Suppress annoying warnings when generating JavaDoc files.
    options.addStringOption('Xdoclint:none', '-quiet')
}

jar {
    manifest.attributes([
            "MixinConfigs": "xaerotrainmap.mixins.json",
    ])
}
